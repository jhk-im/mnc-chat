plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

kapt {
    correctErrorTypes = true
}

// Note: 2 digits max for each value
ext.versionMajor = 1
ext.versionMinor = 3
ext.versionPatch = 2

project.android.buildTypes.all { buildType ->
    buildType.javaCompileOptions.annotationProcessorOptions.arguments =
            [
                    validateEpoxyModelUsage: String.valueOf(buildType.name == 'debug')
            ]
}

// map for the version codes last digit
// x86 must have greater values than arm
// 64 bits have greater value than 32 bits
ext.abiVersionCodes = ["armeabi-v7a": 1, "arm64-v8a": 2, "x86": 3, "x86_64": 4].withDefault { 0 }

def buildNumber = System.env.BUILDKITE_BUILD_NUMBER as Integer ?: 0

android {
    // Due to a bug introduced in Android gradle plugin 3.6.0, we have to specify the ndk version to use
    // Ref: https://issuetracker.google.com/issues/144111441
    ndkVersion "21.3.6528147"

    compileSdk versions.compileSdk

    defaultConfig {
        applicationId "im.vector.app"
        // Set to API 21: see #405
        minSdk versions.minSdk
        targetSdk versions.targetSdk
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "app_name", "MNC Chat DBG"

            resValue "bool", "debug_mode", "true"
            buildConfigField "boolean", "LOW_PRIVACY_LOG_ENABLE", "false"
            // Set to true if you want to enable strict mode in debug
            buildConfigField "boolean", "ENABLE_STRICT_MODE_LOGS", "false"

            signingConfig signingConfigs.debug
        }

        release {
            resValue "string", "app_name", "MNC Chat"

            resValue "bool", "debug_mode", "false"
            buildConfigField "boolean", "LOW_PRIVACY_LOG_ENABLE", "false"
            buildConfigField "boolean", "ENABLE_STRICT_MODE_LOGS", "false"

            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                // We do not activate obfuscation as it makes it hard then to read crash reports, and it's a bit useless on an open source project :)
                obfuscate false
                optimizeCode true
                proguardFiles 'proguard-rules.pro'
            }
        }
    }

    lintOptions {
        lintConfig file("lint.xml")

        checkDependencies true
        abortOnError true
    }

    compileOptions {
        sourceCompatibility versions.sourceCompat
        targetCompatibility versions.targetCompat
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation project(":matrix-sdk-android")
    implementation project(":matrix-sdk-android-rx")
    implementation project(":diff-match-patch")
    implementation project(":multipicker")
    implementation project(":attachment-viewer")
    implementation project(":library:ui-styles")
    implementation 'androidx.multidex:multidex:2.0.1'

    implementation libs.jetbrains.kotlinStdlibJdk7
    implementation libs.jetbrains.coroutinesCore
    implementation libs.jetbrains.coroutinesAndroid

    implementation libs.androidx.recyclerview
    implementation libs.androidx.appCompat
    implementation libs.androidx.fragmentKtx
    implementation libs.androidx.constraintLayout
    implementation libs.androidx.core

    // Log
    implementation libs.jakewharton.timber

    // rx
    implementation libs.rx.rxKotlin
    implementation libs.rx.rxAndroid
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'
    // RXBinding
    implementation libs.jakewharton.rxbinding
    implementation libs.jakewharton.rxbindingAppcompat
    implementation libs.jakewharton.rxbindingMaterial

    // Functional Programming
    implementation libs.arrow.core

    // DI
    implementation libs.dagger.dagger
    kapt libs.dagger.daggerCompiler

    // Pref
    implementation libs.androidx.preferenceKtx

    // UI
    implementation libs.google.material

    // Alerter
    implementation 'com.tapadoo.android:alerter:7.0.1'

    // Image
    implementation libs.github.glide
    kapt libs.github.glideCompiler

    // Emoji
    implementation "androidx.emoji:emoji-appcompat:1.1.0"
    implementation libs.vanniktech.emojiMaterial
    implementation libs.vanniktech.emojiGoogle


    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}